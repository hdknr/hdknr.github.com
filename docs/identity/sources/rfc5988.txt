==============================
RFC5988:Web Linking
==============================

Based on http://tools.ietf.org/html/rfc5988

.. contents:: TOC

Abstract
==========

This document specifies relation types for :term:`Web links`, and defines a
registry for them.  It also defines the use of such links in HTTP
headers with the Link header field.


1. Introduction
====================================

( http://tools.ietf.org/html/rfc5988#section-1 )

A means of indicating the relationships between resources on the Web,
as well as indicating the type of those relationships, has been
available for some time in HTML [W3C.REC-html401-19991224], and more
recently in Atom [RFC4287].  
These mechanisms, although conceptually similar, are separately specified.  
However, 
links between resources need not be format specific; 
it can be useful to have :term:`typed links` 
that are independent of their serialisation, 
especially when a resource has representations in multiple formats.

To this end, 
this document defines a framework for :term:`typed links` 
that isn't specific to a particular serialisation or application.  
It does so by redefining the :term:`link relation registry` 
established by Atom to have a broader domain, 
and adding to it the relations that are defined by HTML.

Furthermore, 
an HTTP header field for conveying :term:`typed links` was
defined in Section 19.6.2.4 of [:term:`RFC2068`], 
but removed from [:term:`RFC2616`], 
due to a lack of implementation experience.  
Since then, 
it has been implemented in some User Agents (e.g., for stylesheets), 
and several additional use cases have surfaced.

Because it was removed, 
the status of the :term:`Link header` is unclear,
leading some to consider minting new application-specific HTTP headers 
instead of reusing it.  
This document addresses this by re-specifying the :term:`Link header` 
as one such serialisation, with updated but backwards-compatible syntax.


5. The Link Header Field
==============================

(  http://tools.ietf.org/html/rfc5988#section-5 )

The Link entity-header field provides 
a means for serialising one or more links in HTTP headers.  
It is semantically equivalent to the <LINK> element in HTML, 
as well as the atom:link feed-level element in Atom [:term:`RFC4287`].

::

  Link           = "Link" ":" #link-value
  link-value     = "<" URI-Reference ">" *( ";" link-param )
  link-param     = ( ( "rel" "=" relation-types )
                 | ( "anchor" "=" <"> URI-Reference <"> )
                 | ( "rev" "=" relation-types )
                 | ( "hreflang" "=" Language-Tag )
                 | ( "media" "=" ( MediaDesc | ( <"> MediaDesc <"> ) ) )
                 | ( "title" "=" quoted-string )
                 | ( "title*" "=" ext-value )
                 | ( "type" "=" ( media-type | quoted-mt ) )
                 | ( link-extension ) )
  link-extension = ( parmname [ "=" ( ptoken | quoted-string ) ] )
                 | ( ext-name-star "=" ext-value )
  ext-name-star  = parmname "*" ; reserved for RFC2231-profiled
                                ; extensions.  Whitespace NOT
                                ; allowed in between.
  ptoken         = 1*ptokenchar
  ptokenchar     = "!" | "#" | "$" | "%" | "&" | "'" | "("
                 | ")" | "*" | "+" | "-" | "." | "/" | DIGIT
                 | ":" | "<" | "=" | ">" | "?" | "@" | ALPHA
                 | "[" | "]" | "^" | "_" | "`" | "{" | "|"
                 | "}" | "~"
  media-type     = type-name "/" subtype-name
  quoted-mt      = <"> media-type <">
  relation-types = relation-type
                 | <"> relation-type *( 1*SP relation-type ) <">
  relation-type  = reg-rel-type | ext-rel-type
  reg-rel-type   = LOALPHA *( LOALPHA | DIGIT | "." | "-" )
  ext-rel-type   = URI

5.5. Examples
^^^^^^^^^^^^^^^^^^^^^

( http://tools.ietf.org/html/rfc5988#section-5.5 )


For example:

::

   Link: <http://example.com/TheBook/chapter2>; rel="previous";
         title="previous chapter"

indicates that "chapter2" is **previous** to this resource in a logical navigation path.

Similarly,

::

   Link: </>; rel="http://example.net/foo"

indicates that the root resource ("/") is related to this resource
with the extension relation type "http://example.net/foo".

The example below shows an instance of the :term:`Link header` encoding multiple links, 
and also the use of :rfc:`RFC 2231 <2231>` encoding 
to encode both non-ASCII characters and language information.

::

   Link: </TheBook/chapter2>;
         rel="previous"; title*=UTF-8'de'letztes%20Kapitel,
         </TheBook/chapter4>;
         rel="next"; title*=UTF-8'de'n%c3%a4chstes%20Kapitel

Here, 
both links have titles encoded in UTF-8, use the German language ("de"), 
and the second link contains the Unicode code point U+00E4 ("LATIN SMALL LETTER A WITH DIAERESIS").

Note that link-values can convey multiple links between the same
target and context IRIs; for example:

::

       Link: <http://example.org/>;
             rel="start http://example.net/relation/other"

Here, 
the link to "http://example.org/" has the registered relation
type ":term:`start`" and the :term:`extension relation type` "http://example.net/relation/other".


